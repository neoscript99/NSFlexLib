<?xml version="1.0" encoding="utf-8"?>
<mx:Accordion xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:file="ns.flex.file.*"
			  creationComplete="cc()" resizeToContent="true">
	<mx:Metadata>
		[Event(name="change")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.rpc.remoting.mxml.RemoteObject;
			import mx.utils.UIDUtil;
			import ns.flex.util.ContainerUtil;
			import ns.flex.util.IOUtil;
			import ns.flex.util.MessageUtil;
			import ns.flex.util.RemoteUtil;

			public const TEMP_ID:String='TEMP_' + UIDUtil.createUID();

			//ex:[new FileFilter("Images", "*.jpg;*.gif;*.png"),new FileFilter("Documents", "*.pdf;*.doc;*.txt")]
			public var fileTypeFilter:Array;
			[Bindable]
			public var maxFileSize:uint=1024 * 1024 * 10;
			[Bindable]
			public var maxNameLength:uint=128;
			[Bindable]
			public var max_fileNumber:uint=5;
			[Bindable]
			private var _fileNumber:uint=0;
			[Bindable]
			private var _ownerId:String;
			[Bindable]
			private var selectValue:Boolean;

			[Bindable]
			private var service:RemoteObject;

			public function set dataProdiver(list:*):void
			{
				downloader.dataProdiver=list;
			}

			public function set destination(destination:String):void
			{
				service=RemoteUtil.createRemoteObject(destination, null, null, true);
				if (_ownerId)
					service.queryByOwner(_ownerId);
			}

			public function get fileNumber():uint
			{
				return _fileNumber;
			}

			public function get info():Object
			{
				return {fileNumber: _fileNumber, ownerId: _ownerId};
			}

			public function get ownerId():String
			{
				return _ownerId;
			}

			public function set ownerId(ownerId:String):void
			{
				eachFileItem(function(item:FileItem):void
				{
					item.remove(false);
				}, false);
				//所属对象id未生成时，没有ownerId,先用临时id，生成后再替换
				_ownerId=ownerId ? ownerId : TEMP_ID; //用一个客户端可以识别的id
				if (service)
					service.queryByOwner(_ownerId);
			}

			private function cc():void
			{
				if (service && _ownerId)
					service.queryByOwner(_ownerId);
			}

			private function countFile(item:DisplayObject):void
			{
				_fileNumber++;
			}

			private function deleteSelected():void
			{
				eachFileItem(removeItem)
			}

			private function eachFileItem(fun:Function, includeDown:Boolean=true):void
			{
				if (vbox)
					ContainerUtil.eachChild(vbox, function(item:DisplayObject):void
					{
						if (item is FileItem)
							fun(item)
					})
				if (downloader && includeDown)
					ContainerUtil.eachChild(downloader, function(item:DisplayObject):void
					{
						if (item is FileItem)
							fun(item)
					})
			}

			private function removeItem(item:FileItem):void
			{
				if (item.checkBox.selected)
				{
					trace('removeItem - ', item.fileName)
					item.remove();
				}
			}

			private function selectFiles():void
			{
				var frl:FileReferenceList=new FileReferenceList;
				frl.browse(fileTypeFilter);
				frl.addEventListener(Event.SELECT, function(e:Event):void
				{
					var str:String='';
					_fileNumber=0;
					eachFileItem(countFile);
					for each (var file:FileReference in frl.fileList)
					{
						if (_fileNumber >= max_fileNumber)
						{
							str=str.concat('文件数不能多于', max_fileNumber, '个\n');
							break;
						}
						try
						{
							if (file.size > maxFileSize)
							{
								str=
									str.concat(file.name, ' 大于',
									IOUtil.getFileSize(maxFileSize), '\n');
								continue;
							}
							if (file.size == 0)
							{
								str=str.concat(file.name, ' 为空文件\n');
								continue;
							}
							if (file.name.length > maxNameLength)
							{
								str=str.concat(file.name, ' 名字长度大于', maxNameLength, '\n');
								continue;
							}
							var fileItem:FileItem=new FileItem;
							fileItem.service=service;
							fileItem.fileId=UIDUtil.createUID();
							fileItem.ownerId=_ownerId;
							fileItem.fileName=file.name;
							fileItem.fileSize=file.size;
							fileItem.dateCreated=new Date();
							fileItem.file=file;
							vbox.addChild(fileItem);
							file.load();
							_fileNumber++;
							dispatchEvent(new Event('change'));
						}
						catch (e:Error)
						{
							trace(e.getStackTrace());
							str=str.concat(file.name, ' ', e.toString());
						}
					}
					if (str.length > 0)
						MessageUtil.showMessage(str, '上传错误信息');
				});
			}

			private function setSelectValue(item:FileItem):void
			{
				item.checkBox.selected=selectValue;
			}
		]]>
	</mx:Script>
	<mx:VBox label="文件上传/下载[{max_fileNumber}×{IOUtil.getFileSize(maxFileSize)}]"
			 paddingLeft="5">
		<file:Downloader id="downloader" service="{service}"
						 dataProdiver="{service.queryByOwner.lastResult}" deletable="true"/>
		<mx:VBox id="vbox"/>
		<mx:HBox width="100%">
			<mx:LinkButton label="全选"
						   click="selectValue=true;eachFileItem(setSelectValue)"/>
			<mx:LinkButton label="全不选"
						   click="selectValue=false;eachFileItem(setSelectValue)"/>
			<mx:LinkButton label="删除选择项"
						   click="MessageUtil.actionYes('确认删除？',deleteSelected)"/>
			<mx:HBox width="100%" horizontalAlign="right">
				<mx:LinkButton label="添加文件" click="selectFiles()"
							   enabled="{_ownerId!=null}"/>
			</mx:HBox>
		</mx:HBox>
	</mx:VBox>
</mx:Accordion>
