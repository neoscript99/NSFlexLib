<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml"
		  title="文件上传/下载[{maxFileNumber}×{IOUtil.getFileSize(maxFileSize)}]"
		  paddingLeft="5" xmlns:file="ns.flex.file.*" creationComplete="cc()">
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.rpc.remoting.mxml.RemoteObject;
			import mx.utils.UIDUtil;
			import ns.flex.util.ContainerUtil;
			import ns.flex.util.IOUtil;
			import ns.flex.util.MessageUtil;
			import ns.flex.util.RemoteUtil;

			[Bindable]
			public var fileNumber:uint=0;
			//ex:[new FileFilter("Images", "*.jpg;*.gif;*.png"),new FileFilter("Documents", "*.pdf;*.doc;*.txt")]
			public var fileTypeFilter:Array;
			[Bindable]
			public var maxFileNumber:uint=5;
			[Bindable]
			public var maxFileSize:uint=1024 * 1024 * 10;
			[Bindable]
			public var maxNameLength:uint=128;
			[Bindable]
			public var ownerId:String;
			[Bindable]
			private var selectValue:Boolean;

			[Bindable]
			private var service:RemoteObject;

			public function set dataProdiver(list:*):void
			{
				downloader.dataProdiver=list;
			}

			public function set destination(destination:String):void
			{
				service=RemoteUtil.createRemoteObject(destination, null, null, false);
			}

			private function cc():void
			{
				service.list({eq: [['ownerId', ownerId]]});
			}

			private function countFile(item:DisplayObject):void
			{
				if (item is FileItem)
					fileNumber++;
			}

			private function deleteSelected():void
			{
				eachFileItem(removeItem)
			}

			private function eachFileItem(fun:Function):void
			{
				ContainerUtil.eachChild(this, fun)
				ContainerUtil.eachChild(downloader, fun)
			}

			private function removeItem(item:DisplayObject):void
			{
				if (item is FileItem && FileItem(item).checkBox.selected)
				{
					trace('removeItem - ', FileItem(item).fileName)
					FileItem(item).remove();
				}
			}

			private function selectFiles():void
			{
				var frl:FileReferenceList=new FileReferenceList;
				frl.browse(fileTypeFilter);
				frl.addEventListener(Event.SELECT, function(e:Event):void
				{
					var str:String='';
					fileNumber=0;
					eachFileItem(countFile);
					for each (var file:FileReference in frl.fileList)
					{
						if (fileNumber >= maxFileNumber)
						{
							str=str.concat('文件数不能多于', maxFileNumber, '个\n');
							break;
						}
						try
						{
							if (file.size > maxFileSize)
							{
								str=
									str.concat(file.name, ' 大于',
									IOUtil.getFileSize(maxFileSize), '\n');
								continue;
							}
							if (file.name.length > maxNameLength)
							{
								str=str.concat(file.name, ' 名字长度大于', maxNameLength, '\n');
								continue;
							}
							var fileItem:FileItem=new FileItem;
							fileItem.service=service;
							fileItem.fileId=UIDUtil.createUID();
							fileItem.ownerId=ownerId;
							fileItem.fileName=file.name;
							fileItem.fileSize=file.size;
							fileItem.dateCreated=new Date();
							fileItem.file=file;
							addChild(fileItem);
							file.load();
						}
						catch (e:Error)
						{
							trace(e.getStackTrace());
							str=str.concat(file.name, ' ', e.toString());
						}
						fileNumber++;
					}
					if (str.length > 0)
						MessageUtil.showMessage(str, '上传错误信息');
				});
			}

			private function setSelectValue(item:DisplayObject):void
			{
				if (item is FileItem)
					FileItem(item).checkBox.selected=selectValue;
			}
		]]>
	</mx:Script>
	<file:Downloader id="downloader" service="{service}"
					 dataProdiver="{service.list.lastResult}" deletable="true"/>
	<mx:ControlBar>
		<mx:Button label="全选" click="selectValue=true;eachFileItem(setSelectValue)"/>
		<mx:Button label="全不选" click="selectValue=false;eachFileItem(setSelectValue)"/>
		<mx:Button label="删除选择项" click="MessageUtil.confirmAction('确认删除？',deleteSelected)"/>
		<mx:HBox width="100%" horizontalAlign="right">
			<mx:Button label="添加文件" click="selectFiles()"/>
		</mx:HBox>
	</mx:ControlBar>
</mx:Panel>
